# Maintainer: Yauhen Kirylau <actionless DOT loveless PLUS aur AT gmail MF com>
# Upstream URL: https://github.com/themix-project/themix-plugin-base16

_pkgname=themix-plugin-base16
_reponame=$_pkgname
pkgname="${_pkgname}-git"
pkgver=1.5.4.r21.g3b05d8b
pkgrel=1
pkgdesc="Import and export plugin for Themix GUI designer to use color palettes from Base16 project"
arch=('x86_64' 'i686')
url="https://github.com/themix-project/${_reponame}"
license=('GPL3')
source=(
	"${_reponame}::git+https://github.com/themix-project/${_reponame}.git#commit=54e3df27d3c90ae5911a1c3371e69be43d92a788"
	0001-Make-oomox-generate-dark-themes.patch
)
md5sums=('SKIP'
         '2e7a4b79411820110e7e135b689da2a2')
depends=(
	'themix-gui'
	'python-pystache'
	'python-yaml'
)
makedepends=(
	'git'
)
optdepends=(
)
options=(
	'!strip'
)
provides=($_pkgname)
conflicts=(
    $_pkgname
    'oomox'
    'oomox-git'
)

prepare() {
	cd "${srcdir}/${_reponame}"
	patch -p1 < ../0001-Make-oomox-generate-dark-themes.patch
}

package() {
	_oomox_dir=/usr/share/oomox
	_plugin_name=base16

	cd "${srcdir}/${_reponame}"
	make -f Makefile_oomox_plugin DESTDIR="${pkgdir}" APPDIR="${_oomox_dir}" PREFIX="/usr" install
	python -O -m compileall "${pkgdir}${_oomox_dir}/plugins/${_plugin_name}" -d "${_oomox_dir}/plugins/${_plugin_name}"
}

# vim: ft=PKGBUILD
