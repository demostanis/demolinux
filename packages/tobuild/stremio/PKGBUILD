# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Contributor: demostanis

pkgname=stremio
pkgver=4.4.168
pkgrel=6
pkgdesc='A one-stop hub for video content aggregation (Movies, TV shows, series, live television or web channels)'
arch=('x86_64')
url='https://www.stremio.com/'
license=('GPL-3.0-only')
depends=('mpv' 'nodejs' 'openssl' 'qt5-base' 'qt5-declarative' 'qt5-webengine'
         'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-translations' 'qt5-webchannel'
         'hicolor-icon-theme')
makedepends=('git' 'cmake' 'librsvg')
source=("git+https://github.com/Stremio/stremio-shell.git#tag=v${pkgver}"
        'git+https://github.com/Ivshti/libmpv.git'
        'git+https://github.com/itay-grudev/SingleApplication.git'
        'git+https://github.com/Ivshti/razerchroma.git'
        "stremio-${pkgver}-server.js"::"https://dl.strem.io/four/v${pkgver}/server.js"
        '010-stremio-do-not-download-server-js.patch'
        '0001-Download-files-with-curl.patch')
sha256sums=('30b65909f100ed31b406280077b54fed9a50602dec28c540c6c0dc0c02b5597e'
            'SKIP'
            'SKIP'
            'SKIP'
            '683de7890a60ab512264b69c9c41609aa515f9b738aef18df4803ef53e17175d'
            'b5eff88b30d8c6030e36ca4949ebf6ff9515efbedc0b9bc748110cd1fbc0671a'
            '452e946e344540330574691d34ef41b32906f42d57220ba57749e122626e774e')

prepare() {
    git -C stremio-shell submodule init
    git -C stremio-shell config --local submodule.deps/libmpv.url "${srcdir}/libmpv"
    git -C stremio-shell config --local submodule.deps/singleapplication.url "${srcdir}/SingleApplication"
    git -C stremio-shell config --local submodule.deps/chroma.url "${srcdir}/razerchroma"
    git -C stremio-shell -c protocol.file.allow='always' submodule update
    
    # install to /usr/share instead of /opt
    sed -i 's,/opt/stremio,/usr/share/stremio,' stremio-shell/release.makefile
    sed -i 's,opt/stremio,usr/share/stremio,' stremio-shell/CMakeLists.txt

    # do not download server.js during 'make'
    ln -sf "../stremio-${pkgver}-server.js" stremio-shell/server.js
    patch -d stremio-shell -Np1 -i "${srcdir}/010-stremio-do-not-download-server-js.patch"
    patch -d stremio-shell -Np1 -i "${srcdir}/0001-Download-files-with-curl.patch"
}

build() {
    make -C stremio-shell -f release.makefile PREFIX="$pkgdir"
}

package() {
    make -C stremio-shell -f release.makefile PREFIX="$pkgdir" install
    
    # streaming server files
    install -D -m644 "stremio-${pkgver}-server.js"    "${pkgdir}/usr/share/stremio/server.js"
    
    # binary and desktop file
    mkdir -p "${pkgdir}/usr/"{bin,share/applications}
    ln -s ../../usr/share/stremio/stremio "${pkgdir}/usr/bin/stremio"
    ln -s ../../../usr/share/stremio/smartcode-stremio.desktop "${pkgdir}/usr/share/applications/smartcode-stremio.desktop"
    
    # icons
    local _file
    local _res
    while read -r -d '' _file
    do
        _res="$(printf '%s' "$_file" | sed 's/\.png$//;s/^.*_//')"
        mkdir -p "${pkgdir}/usr/share/icons/hicolor/${_res}x${_res}/apps"
        ln -s ../../../../../../usr/share/stremio/icons/smartcode-stremio_${_res}.png \
              "${pkgdir}/usr/share/icons/hicolor/${_res}x${_res}/apps/smartcode-stremio.png"
        ln -s ../../../../../../usr/share/stremio/icons/smartcode-stremio-tray_${_res}.png \
              "${pkgdir}/usr/share/icons/hicolor/${_res}x${_res}/apps/smartcode-stremio-tray.png"
    done < <(find "${pkgdir}/usr/share/stremio/icons" -maxdepth 1 -type f -name 'smartcode-stremio_*.png' -print0)
}
