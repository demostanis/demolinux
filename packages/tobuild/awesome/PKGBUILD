# Maintainer: Oleg Shparber <trollixx+aur@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta
# URL: https://github.com/trollixx/aur-packages
# Upstream: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/awesome

_pkgname=awesome
pkgname=${_pkgname}
pkgver=4.3.1654.g8b1f8958b
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'libxdg-basedir' 'libxkbcommon-x11'
         'lua' 'lua-lgi' 'pango' 'startup-notification' 'xcb-util-cursor'
         'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm' 'libxfixes')
makedepends=('asciidoctor' 'cmake' 'docbook-xsl' 'git' 'imagemagick' 'ldoc'
             'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'xcb-util-errors: for pretty-printing of X11 errors'
            'librsvg: for displaying SVG files without scaling artifacts'
)
provides=('notification-daemon' 'awesome')
conflicts=('awesome')
backup=('etc/xdg/awesome/rc.lua')
source=("$pkgname::git+https://github.com/awesomeWM/awesome.git"
	0001-Allow-specifying-a-y-offset-for-notifications.patch
	0002-Expose-surface.patch
	0003-Make-notification-icons-use-the-right-icon-theme.patch
	0004-Ensure-the-boot-splash-is-hidden.patch
	0005-Add-margins-to-wicon.patch)
md5sums=('SKIP'
         'bdf30656a639bd0d2e9ce740882dec59'
         'c987300e07e58d6dbae37e40734a8f56'
         '5ac3c933b78a7a77fa5d3d5b5c9e7268'
         '953574a6594fea8ddb78f2d36fba5011'
         'a06a41d70750b3fdcdf5a111ac469f10')
_LUA_VER=5.4

pkgver() {
  cd $pkgname
  git describe | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $pkgname
  git apply < ../0001-Allow-specifying-a-y-offset-for-notifications.patch
  git apply < ../0002-Expose-surface.patch
  git apply < ../0003-Make-notification-icons-use-the-right-icon-theme.patch
  git apply < ../0004-Ensure-the-boot-splash-is-hidden.patch
  git apply < ../0005-Add-margins-to-wicon.patch
}

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_INCLUDE_DIR=/usr/include/lua${_LUA_VER} \
    -DLUA_LIBRARY=/usr/lib/liblua.so.${_LUA_VER} \
    -DLUA_EXECUTABLE=/usr/bin/lua${_LUA_VER}
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname/awesome.desktop \
    "$pkgdir/usr/share/xsessions/awesome.desktop"
}
