# Maintainer: Matt Harrison <matt@harrison.us.com>
# Contributor: David Hummel <hummeltech@sherpaguru.com>
# Maintained at: https://github.com/matt-h/aur-pkgbuilds or https://codeberg.org/matt/aur-pkgbuilds

pkgname=firefox-bitwarden
pkgver=2025.5.1
pkgrel=6
pkgdesc='Bitwarden browser extension for Firefox'
arch=('any')
url='https://github.com/bitwarden/clients'
license=('GPL-3.0-or-later')
optdepends=('firefox')
groups=('firefox-addons')
pkgdesc='Bitwarden browser extension for Firefox'
makedepends=('nodejs' 'npm' 'unzip' 'zip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bitwarden/clients/archive/refs/tags/browser-v${pkgver}.tar.gz" 0001-personal-nitpicks.patch)
b2sums=('07259b5428096a816f4f182be63022fbc6cdcd70d198c4705fea791fd7897ef283c3d18cfd01569907c99f9a54ff7506c52908fab00229bfca9aeeaf078c7f53'
        '712f298d65730a1161cb371a836d7ae0cc2bd21a6682b72fdd667679727852af028bd7c2646fa9f15dbe8096cd51cd0788dc038f4cecfb16abf7db91c1c832a2')

prepare() {
  cd "${srcdir}/clients-browser-v${pkgver}" || exit
  patch -p1 < ../0001-personal-nitpicks.patch
  npm ci
}

build() {
  cd "${srcdir}/clients-browser-v${pkgver}/apps/browser" || exit
  npm run dist:firefox
}

check() {
  cd "${srcdir}/clients-browser-v${pkgver}/apps/browser" || exit
  npm run test
}

package() {
  install -Dm644 "${srcdir}/clients-browser-v${pkgver}/apps/browser/dist/dist-firefox.zip" "${pkgdir}/usr/lib/firefox/browser/extensions/{446900e4-71c2-419f-a6a7-df9c091e268b}.xpi"
}
