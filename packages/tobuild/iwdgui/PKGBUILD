# Maintainer: Johannes Willem (Hans) Fernhout <hfern@fernhout.info>
pkgname="iwdgui"
pkgver="0.3.0"
pkgrel=1
pkgdesc="A graphical frontend for IWD, Intel's iNet Wireless Daemon"
arch=("any")
url="https://gitlab.com/hfernh/iwdgui"
license=("BSD")
depends=("python-gobject" "gtk3" "python-netifaces" "dbus-python")
makedepends=(python-build python-setuptools python-installer python-wheel)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('fd4f02a778d2edf3f9d6552744a5d2427fb45f78ff0ed8eb15654b5fdb2a3ef2')

build() {
    cd "${pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${pkgname}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
