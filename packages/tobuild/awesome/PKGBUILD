# Maintainer: Oleg Shparber <trollixx+aur@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta
# URL: https://github.com/trollixx/aur-packages
# Upstream: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/awesome

_pkgname=awesome
pkgname=${_pkgname}
pkgver=4.3.1683.g691e36425
pkgrel=4
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'libxdg-basedir' 'libxkbcommon-x11'
         'lua' 'lua-lgi' 'pango' 'startup-notification' 'xcb-util-cursor'
         'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm' 'libxfixes')
makedepends=('asciidoctor' 'cmake' 'docbook-xsl' 'git' 'imagemagick' 'ldoc'
             'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'xcb-util-errors: for pretty-printing of X11 errors'
            'librsvg: for displaying SVG files without scaling artifacts'
)
provides=('notification-daemon' 'awesome')
conflicts=('awesome')
backup=('etc/xdg/awesome/rc.lua')
source=("$pkgname::git+https://github.com/awesomeWM/awesome.git#commit=691e36425a645e54402cb04efc4c2b00d73051bd"
	0001-Allow-specifying-a-y-offset-for-notifications.patch
	0002-Expose-surface.patch
	0003-Make-notification-icons-use-the-right-icon-theme.patch
	0004-Ensure-the-boot-splash-is-hidden.patch
	0005-Add-margins-to-wicon.patch
        0006-push-new-windows-in-the-stack-after-the-focused-wind.patch
        0007-add-client-insert_before.patch
        0008-rlwrap.patch)
md5sums=('SKIP'
         'bdf30656a639bd0d2e9ce740882dec59'
         'c987300e07e58d6dbae37e40734a8f56'
         '5ac3c933b78a7a77fa5d3d5b5c9e7268'
         '953574a6594fea8ddb78f2d36fba5011'
         'a06a41d70750b3fdcdf5a111ac469f10'
         'e7bbb703b74f17625f37dfad66201cea'
         '90340481f9ae0e41241c7d02ad611b66'
         '31cb43c7cd5bcf202a267c6e813e68ea')
_LUA_VER=5.4

pkgver() {
  cd $pkgname
  git describe | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $pkgname
  for patch in ../*.patch; do
    git apply < "$patch"
  done
}

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname \
    -DGENERATE_DOC=OFF \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_INCLUDE_DIR=/usr/include/lua${_LUA_VER} \
    -DLUA_LIBRARY=/usr/lib/liblua.so.${_LUA_VER} \
    -DLUA_EXECUTABLE=/usr/bin/lua${_LUA_VER}
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname/awesome.desktop \
    "$pkgdir/usr/share/xsessions/awesome.desktop"
}
